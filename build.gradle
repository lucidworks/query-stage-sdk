plugins {
    id 'nebula.release' version '14.1.1' apply false
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

allprojects {
    apply plugin: 'nebula.release'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven {
            credentials {
                username = "${lucidArtifactoryUsername}"
                password = "${lucidArtifactoryPassword}"
            }
            authentication {
                basic(BasicAuthentication)
            }
            url "https://artifactory.lucidworks.com/artifactory/fusion-dev/"
        }
        mavenCentral()
        mavenLocal()
        maven {
            url "https://artifactory.lucidworks.com/artifactory/public-artifacts/"
        }
    }

    dependencies {
        testImplementation "junit:junit:${junitVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            mavenLocal()
            maven {
                url project.hasProperty('repositoryUrl') ? project.property('repositoryUrl') : ''
                credentials {
                    username project.hasProperty('lucidArtifactoryUsername') ? project.property('lucidArtifactoryUsername') : ''
                    password project.hasProperty('lucidArtifactoryPassword') ? project.property('lucidArtifactoryPassword') : ''
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}
